plugins {
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'groovy'
}

group = "com.github.ubadahj"
version = "0.0.5"

repositories {
    mavenCentral()
    jcenter()
    google()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'com.squareup:javapoet:1.9.0'
    implementation 'com.android.tools.build:gradle:4.2.2'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"


group = 'net.idik.lib.security'
archivesBaseName = 'cipher.so'
version = '0.0.5'

task zipNativeArchive(type: Zip) {
    destinationDir project.file("build/libs")
    archiveName "$archivesBaseName-${version}.jarx"
    from project.zipTree("build/libs/$archivesBaseName-${version}.jar")
    include "META-INF/**"
    include "net/**"
    from "${project.rootProject.file("devso").canonicalPath}"
    include "src/main/**"
    include "CMakeLists.txt"
    exclude "src/main/cpp/include/extern-keys.h"
    exclude "src/main/java/net/idik/lib/cipher/so/devso/**"
    exclude "src/main/jni"
    exclude "src/main/res/**"
    exclude "src/main/AndroidManifest.xml"

    doLast {
        def originJar = project.file("build/libs/$archivesBaseName-${version}.jar")
        def xJar = project.file("build/libs/$archivesBaseName-${version}.jarx")
        originJar.delete()
        xJar.renameTo(originJar)
    }

}

tasks['jar'].finalizedBy zipNativeArchive

gradlePlugin {
    plugins {
        CipherSoPlugin {
            id = "com.github.ubadahj.cipherso"
            implementationClass = "net.idik.lib.cipher.so.plugin.CipherSoPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/UbadahJ/Cipher.so'
    vcsUrl = 'https://github.com/UbadahJ/Cipher.so'
    description = 'A sample way to store secure message in .so librarly'
    tags = ['cipher', 'encryption']

    plugins {
        CipherSoPlugin {
            displayName = 'Cipher for Android'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}